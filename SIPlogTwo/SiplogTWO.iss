; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
#include ReadReg(HKEY_LOCAL_MACHINE,'Software\Sherlock Software\InnoTools\Downloader','ScriptPath','') 
[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{0E73F7C9-6242-4E42-909E-C8F97051CA52}
AppName=SIPlogTwo
AppVersion=1.1
;AppVerName=SIPlogtwo 1
AppPublisher=Greg Palmer
AppPublisherURL=gregp203.github.com
AppSupportURL=gregp203.github.com
AppUpdatesURL=gregp203.github.com
DefaultDirName={pf}\SIPlogTwo
DefaultGroupName=SIPlogTwo
AllowNoIcons=yes
OutputDir=C:\Users\palmerg\Desktop
OutputBaseFilename=SIPlogTwo-setup
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: mypAssociation; Description: "Associate ""sls"" extension"; GroupDescription: File extensions:

[Files]
Source: "C:\Users\palmerg\source\repos\SIPlogTwo\SIPlogTwo\bin\Release\SIPlogTwo.exe"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\SIPlogTwo"; Filename: "{app}\SIPlogTwo.exe"
Name: "{group}\{cm:UninstallProgram,SIPlogTwo}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\SIPlogTwo"; Filename: "{app}\SIPlogTwo.exe"; Tasks: desktopicon

[Run]
Filename: "{app}\SIPlogTwo.exe"; Description: "{cm:LaunchProgram,SIPlogTwo}"; Flags: nowait postinstall skipifsilent

[Registry]
Root: HKCR; Subkey: ".log";                             ValueData: "SIPlogTwo";          Flags: uninsdeletevalue; ValueType: string;  ValueName: ""
Root: HKCR; Subkey: "SIPlogTwo";                     ValueData: "Program SIPlogTwo";  Flags: uninsdeletekey;   ValueType: string;  ValueName: ""
Root: HKCR; Subkey: "SIPlogTwo\DefaultIcon";         ValueData: "{app}\SIPLOGTWO.EXE,0";               ValueType: string;  ValueName: ""
Root: HKCR; Subkey: "SIPlogTwo\shell\open\command";  ValueData: """{app}\SIPLOGTWO.EXE"" ""%1""";  ValueType: string;  ValueName: ""

[Code]
function Framework45IsNotInstalled(): Boolean;
var
  bSuccess: Boolean;
  regVersion: Cardinal;
begin
  Result := True;

  bSuccess := RegQueryDWordValue(HKLM, 'Software\Microsoft\NET Framework Setup\NDP\v4\Full', 'Release', regVersion);
  if (True = bSuccess) and (regVersion >= 378389) then begin
    Result := False;
  end;

end;

procedure InitializeWizard;
begin
  if Framework45IsNotInstalled() then
  begin
    ITD_Init;
    ITD_AddFile('http://go.microsoft.com/fwlink/?LinkId=397707', ExpandConstant('{tmp}\NetFrameworkInstaller.exe'));
    ITD_DownloadAfter(wpReady);
  end;
end;

procedure InstallFramework;
var
  StatusText: string;
  ResultCode: Integer;
begin
  StatusText := WizardForm.StatusLabel.Caption;
  WizardForm.StatusLabel.Caption := 'Installing .NET Framework 4.5.2. This might take a few minutes...';
  WizardForm.ProgressGauge.Style := npbstMarquee;
  try
    if not Exec(ExpandConstant('{tmp}\NetFrameworkInstaller.exe'), '/passive /norestart', '', SW_SHOW, ewWaitUntilTerminated, ResultCode) then
    begin
      MsgBox('.NET installation failed with code: ' + IntToStr(ResultCode) + '.', mbError, MB_OK);
    end;
  finally
    WizardForm.StatusLabel.Caption := StatusText;
    WizardForm.ProgressGauge.Style := npbstNormal;

    DeleteFile(ExpandConstant('{tmp}\NetFrameworkInstaller.exe'));
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  case CurStep of
    ssPostInstall:
      begin
        if Framework45IsNotInstalled() then
        begin
          InstallFramework();
        end;
      end;
  end;
end;